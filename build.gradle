plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'jacoco'
	id 'checkstyle'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.yaml:snakeyaml:2.2'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.rest-assured:rest-assured:5.3.2'
}

sourceSets {
	integrationTest {
		java.srcDir file('src/integrationTest/java')
		resources.srcDir file('src/integrationTest/resources')
		compileClasspath += sourceSets.main.output + sourceSets.test.output
		runtimeClasspath += sourceSets.main.output + sourceSets.test.output
	}
	systemIntegrationTest {
		java.srcDir file('src/systemIntegrationTest/java')
		resources.srcDir file('src/systemIntegrationTest/resources')
		compileClasspath += sourceSets.main.output + sourceSets.test.output
		runtimeClasspath += sourceSets.main.output + sourceSets.test.output
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
	systemIntegrationTestImplementation.extendsFrom testImplementation
	systemIntegrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('integrationTest', Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	mustRunAfter test
	useJUnitPlatform()
	testLogging {
		events "PASSED", "FAILED", "SKIPPED"
	}
}

tasks.register('systemIntegrationTest', Test) {
	testClassesDirs = sourceSets.systemIntegrationTest.output.classesDirs
	classpath = sourceSets.systemIntegrationTest.runtimeClasspath
	mustRunAfter integrationTest
	useJUnitPlatform()
	testLogging {
		events "PASSED", "FAILED", "SKIPPED"
	}
}

jacoco {
	toolVersion = '0.8.7'
}

jacocoTestReport {
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					"com/example/demo/calculator/Constants.class",
					"com/example/demo/request/CalculateRequest.class",
					"com/example/demo/CalculatorApplication.class"
			])
		}))
	}
	reports {
		xml.required.set(true)
		html.outputLocation.set(file("build/reports/jacoco"))
		xml.outputLocation.set(file("build/reports/jacoco/jacocoTestReport.xml"))
	}
}

tasks.named('test') {
	useJUnitPlatform()
	testLogging {
		events 'passed', 'skipped', 'failed'
	}
	finalizedBy jacocoTestReport
}

checkstyle {
	configFile = file("config/checkstyle/google_checks.xml")
	toolVersion = '10.12.4'
	reportsDir = file("build/checkstyle")
	showViolations = true
}

tasks.withType(Checkstyle).configureEach {
	reports {
		xml.required.set(false)
		html.required.set(true)
	}
}
